apply plugin: 'com.android.library'

def DEFAULT_COMPILE_SDK_VERSION             = 30
def DEFAULT_BUILD_TOOLS_VERSION             = "30.0.3"
def DEFAULT_TARGET_SDK_VERSION              = 30
def DEFAULT_KOTLIN_VERSION                  = "1.3.72"
def DEFAULT_ANDROID_MIN_SDK_VERSION         = 21

android {
    namespace = "com.twiliorn.library"
    compileSdkVersion rootProject.hasProperty('compileSdkVersion') ? rootProject.compileSdkVersion : DEFAULT_COMPILE_SDK_VERSION
    buildToolsVersion rootProject.hasProperty('buildToolsVersion') ? rootProject.buildToolsVersion : DEFAULT_BUILD_TOOLS_VERSION

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        minSdkVersion rootProject.hasProperty('minSdkVersion') ? rootProject.minSdkVersion : DEFAULT_ANDROID_MIN_SDK_VERSION
        targetSdkVersion rootProject.hasProperty('targetSdkVersion') ? rootProject.targetSdkVersion : DEFAULT_TARGET_SDK_VERSION
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Specify that we want to split up the APK based on ABI
    splits {
        abi {
            // Enable ABI split
            enable true

            // Clear list of ABIs
            reset()

            // Specify each architecture currently supported by the Video SDK
            include "armeabi-v7a", "arm64-v8a", "x86", "x86_64"

            // Specify that we do not want an additional universal SDK
            universalApk false
        }
    }
}

dependencies {
    def kotlinVersion = rootProject.hasProperty('kotlinVersion') ? rootProject.kotlinVersion : DEFAULT_KOTLIN_VERSION

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Use AndroidX libraries
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.core:core:1.13.1"

    // Include Twilio Video SDK
    implementation "com.twilio:video-android:7.6.1"
    
    // React Native dependency from node_modules
    implementation "com.facebook.react:react-native:+"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
}
